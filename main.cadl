import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-dpg";

using Cadl.Reflection;
using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.DPG;

@service({
  title: "BatchServiceClient",
  version: "2022-10-01.16.0",
})

@versioned(BatchService.Versions)
@versionedDependency([[BatchService.Versions.v2022_10_01, Azure.Core.Versions.v1_0_Preview_2]])

@doc("A client for issuing REST requests to the Azure Batch service.")
namespace BatchService;

enum Versions {
  v2022_10_01: "2022-10-01.16.0",
}


@operationGroup
@tag("Files")
interface File {
  @doc("Returns the content of the specified Compute Node file.")
  @route("/pools/{poolId}/nodes/{nodeId}/files/{filePath}")
  @get
  GetFromComputeNode is Azure.Core.Foundations.Operation<
    BatchPoolFileClientRequestHeaders,
    {
      @header("content-type") contentType: "application/octet-stream";

      @doc("The client-request-id provided by the client during the request. This will be returned only if the return-client-request-id parameter was set to true.")
      @header
      @format("uuid")
      "client-request-id"?: string;

      @doc("A unique identifier for the request that was made to the Batch service. If a request is consistently failing and you have verified that the request is properly formulated, you may use this value to report the error to Microsoft. In your report, include the value of this request ID, the approximate time that the request was made, the Batch Account against which the request was made, and the region that Account resides in.")
      @header
      @format("uuid")
      "request-id"?: string;

      @doc("The ETag HTTP response header. This is an opaque string. You can use it to detect whether the resource has changed between requests. In particular, you can pass the ETag to one of the If-Modified-Since, If-Unmodified-Since, If-Match or If-None-Match headers.")
      @header
      ETag?: string;

      @doc("The time at which the resource was last modified.")
      @header
      @format("date-time-rfc1123")
      "Last-Modified"?: string;

      @header
      @doc("The file creation time.")
      @format("date-time-rfc1123")
      "ocp-creation-time"?: string;

      @header
      @doc("Whether the object represents a directory.")
      "ocp-batch-file-isdirectory": boolean;

      @header
      @doc("The URL of the file.")
      "ocp-batch-file-url": string;

      @header
      @doc("The file mode attribute in octal format.")
      "ocp-batch-file-mode": string;

      @header
      @doc("The length of the file.")
      "Content-Length": int64;

      @body
      @doc("A response containing the file content.")
      file: bytes;
    }
  >;
}

@doc("Common header parms for Pool related File operartions")
model BatchPoolFileClientRequestHeaders {
  @doc("The ID of the Pool that contains the Compute Node.")
  @path
  poolId: string;

  @doc("The ID of the Compute Node from which you want to delete the file.")
  @path
  nodeId: string;

  @doc("The path to the file or directory that you want to delete.")
  @path
  filePath: string;

  @doc("""
The byte range to be retrieved. The default is to retrieve the entire file. The
format is bytes=startRange-endRange.
""")
  @header
  "ocp-range"?: string;

  @doc("""
The maximum number of items to return in the response. A maximum of 1000
applications can be returned.
""")
  @query
  timeOut?: int32 = 30;

  @doc("""
The caller-generated request identity, in the form of a GUID with no decoration
such as curly braces, e.g. 9C4D50EE-2D56-4CD3-8152-34347DC9F2B0.
""")
  @header
  @format("uuid")
  "client-request-id"?: string;

  @doc("Whether the server should return the client-request-id in the response.")
  @header
  "return-client-request-id"?: boolean = false;

  @doc("""
The time the request was issued. Client libraries typically set this to the
current system clock time; set it explicitly if you are calling the REST API
directly.
""")
  @header
  @format("date-time-rfc1123")
  "ocp-date"?: string;

  @doc("""
  A timestamp indicating the last modified time of the resource known to the
  client. The operation will be performed only if the resource on the service has
  been modified since the specified time.
  """)
  @header
  @format("date-time-rfc1123")
  "If-Modified-Since"?: string;

  @doc("""
  A timestamp indicating the last modified time of the resource known to the
  client. The operation will be performed only if the resource on the service has
  not been modified since the specified time.
  """)
  @header
  @format("date-time-rfc1123")
  "If-Unmodified-Since"?: string;
}
